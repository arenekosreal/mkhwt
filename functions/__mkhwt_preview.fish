function __mkhwt_preview -a theme_root -a temp_dir -a preview
    mkdir -p "$theme_root/preview"
    if test -z "$preview"
        __mkhwt_log info "Generating dummy preview directory..."
        set -l PREVIEW_WIDTH 1080
        set -l PREVIEW_HEIGHT 2160

        set -l ICONS "$temp_dir/icons/"*.png
        if test (count $ICONS) -gt 0
            __mkhwt_log info "Generating preview for icons..."
            set -l WIDTH_ICON_COUNT 3
            set -l HEIGHT_ICON_COUNT 5
            set -l ICON_COUNT_TO_PREVIEW (math $WIDTH_ICON_COUNT x $HEIGHT_ICON_COUNT)
            set -l icons_to_preview
            if test $ICON_COUNT_TO_PREVIEW -gt (count $ICONS)
                set icons_to_preview $ICONS
            else
                while test (count $icons_to_preview) -lt $ICON_COUNT_TO_PREVIEW
                    set -l icon_to_preview (random choice $ICONS)
                    if not contains $icon_to_preview $icons_to_preview
                        set -a icons_to_preview $icon_to_preview
                    end
                end
            end
            magick montage \
                -size "$PREVIEW_WIDTH"x"$PREVIEW_HEIGHT" \
                -gravity center \
                -tile "$WIDTH_ICON_COUNT"x"$HEIGHT_ICON_COUNT" \
                $icons_to_preview "$theme_root/preview/preview_icons_0.png"
        end

        set -l FONTS "$theme_root/fonts/"*.ttf
        if test (count $FONTS) -gt 0
            __mkhwt_log info "Generating preview for fonts..."
            set -l PREVIEW_TEXT "abcdefghijklmnopqrstuvwxyz\nABCDEFGHIJKLMNOPQRSTUVWXYZ\n0123456789\n龙跳天门，虎卧凰阁\n我能吞下玻璃而不伤身体"
            magick \
                -size "$PREVIEW_WIDTH"x"$PREVIEW_HEIGHT" \
                -background white \
                -gravity center \
                -font (random choice $FONTS) \
                caption:$PREVIEW_TEXT \
                "$theme_root/preview/preview_fonts_0.png"
        end

        set -l unlock_wallpaper "$theme_root/wallpaper/unlock_wallpaper_0."*
        set -l previews "$theme_root/preview/preview_"*
        if test (count $unlock_wallpaper) = 1
            __mkhwt_log info "Copying unlock wallpaper as preview of unlock..."
            cp "$unlock_wallpaper[1]" "$theme_root/preview/preview_unlock_0"(path extension "$unlock_wallpaper[1]")
        else if test (count $previews) -gt 0
            set -l chosen_preview (random choice $previews)
            __mkhwt_log warn "Reusing existing preview $chosen_preview as preview of unlock..."
            cp "$chosen_preview" "$theme_root/preview/preview_unlock_0"(path extension "$chosen_preview")
        else
            __mkhwt_log warn "Faking an empty preview as preview of unlock..."
            magick -size "$PREVIEW_WIDTH"x"$PREVIEW_HEIGHT" -gravity center label:"Unlock preview generated by mkhwt." "$theme_root/preview/preview_unlock_0.png"
        end
    else if not test -d "$preview"
        __mkhwt_log error "`$preview` is not a valid preview directory."
        return 1
    else
        cp -r "$preview/." "$theme_root/preview"
    end
end
