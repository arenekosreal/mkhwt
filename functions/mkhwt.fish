function mkhwt --description "Create customized Huawei theme based on icon pack and fonts, etc."
    set -l options (fish_opt --short h --long help)
    set -a options (fish_opt --short i --long icons --optional-val)
    set -a options (fish_opt --short I --long icon-pack --optional-val)
    set -a options (fish_opt --short c --long icon-map --optional-val)
    set -a options (fish_opt --short f --long fonts --optional-val)
    set -a options (fish_opt --short w --long wallpaper --optional-val)
    set -a options (fish_opt --short u --long unlock --optional-val)
    set -a options (fish_opt --short p --long preview --optional-val)
    set -a options (fish_opt --short r --long framework-res --optional-val)
    set -a options (fish_opt --short R --long framework-res-hwext --optional-val)
    set -a options (fish_opt --short t --long title --required-val)
    set -a options (fish_opt --short T --long title-cn --optional-val)
    set -a options (fish_opt --short a --long author --optional-val)
    set -a options (fish_opt --short d --long designer --optional-val)
    set -a options (fish_opt --short s --long screen --optional-val)
    set -a options (fish_opt --short v --long version --optional-val)
    set -a options (fish_opt --short o --long font --optional-val)
    set -a options (fish_opt --short O --long font-cn --optional-val)
    set -a options (fish_opt --short b --long briefinfo --optional-val)
    set -a options (fish_opt --short m --long format-type --optional-val)
    argparse $options -- $argv
    or return
    set -e options

    set -l COMMAND (status function)
    set -l TEMP_DIR (mktemp --tmpdir -d "$COMMAND.XXXXXX")

    set -l DEFAULT_SCREEN XXHD
    set -l DEFAULT_VERSION 12.0.0
    set -l DEFAULT_FONT Default
    set -l DEFAULT_FONT_CN 默认
    set -l DEFAULT_BRIEFINFO "A theme generated by $COMMAND."
    set -l FORMAT_TYPES 1 2

    if set -ql _flag_h
        begin
            set -l function_info (functions -Dv (status function))
            echo "  $COMMAND <--title TITLE> [OPTION]"
            echo "  $function_info[5]"
            echo "  See here for more info about theme:"
            echo "      https://developer.huawei.com/consumer/en/doc/content/themes-specification-0000001160896163"
            echo "  Required Options:"
            echo "      -t | --title:               The name of theme."
            echo "  Optional Options:"
            echo "      -i | --icons:               Where is the customized icons."
            echo "      -I | --icon-pack:           The path to icon pack apk or the icon pack appid."
            echo "      -c | --icon-map:            The json file contains info about which icon should be used for a appid."
            echo "      -f | --fonts:               Where is the customized fonts."
            echo "      -w | --wallpaper:           Where is the wallpaper of theme."
            echo "      -p | --preview:             Where is the preview images. We will generate a dummy one if not provided."
            echo "      -u | --unlock:              Where is the unlock screen. We will generate a dummy one if not provided."
            echo "      -r | --framework-res:       Where is the framework-res of theme. We will generate a dummy one if not provided."
            echo "      -R | --framework-res-hwext: Where is the framework-res-hwext of theme. We will generate a dummy one if not provided."
            echo "      -T | --title-cn:            The name of theme in Chinese. We will use value of --title if not provided."
            echo "      -a | --author:              The author of theme."
            echo "      -d | --designer:            The designer of theme."
            echo "      -s | --screen:              The screen size suitable for the theme. Defaults to `$DEFAULT_SCREEN`."
            echo "      -v | --version:             The version of the theme. Defaults to `$DEFAULT_VERSION`."
            echo "      -o | --font:                The font of the theme. Defaults to `$DEFAULT_FONT`."
            echo "      -O | --font-cn:             The font of the theme in Chinese. Defaults to `$DEFAULT_FONT_CN`."
            echo "      -b | --briefinfo:           The brief info of the theme. Defaults to `$DEFAULT_BRIEFINFO`."
            echo "      -W | --wallpaper-dark:      If the theme has dark wallpaper."
            echo "      -m | --format-type:         The formatType of theme. unlock theme is 1, icon theme is 2, no needed for other theme types."
            echo "  Other:"
            echo "      -h | --help:                Print this help."
        end >&2
        return
    end

    if test -z "$_flag_t"
        __mkhwt_log info "You must specify title by -t/--title argument. See `$COMMAND --help` for more info."
        return 1
    end

    set -l THEME_ROOT "$TEMP_DIR/theme_slash"
    mkdir -p "$THEME_ROOT"

    __mkhwt_fonts "$THEME_ROOT" "$_flag_f"
    or return

    __mkhwt_unlock "$THEME_ROOT" "$_flag_u"
    or return

    __mkhwt_wallpaper "$THEME_ROOT" "$_flag_w"
    or return

    __mkhwt_icons "$THEME_ROOT" "$TEMP_DIR" "$_flag_i" "$_flag_I" "$_flag_c"
    or return

    __mkhwt_framework-res "$THEME_ROOT" "$TEMP_DIR" "$_flag_r"
    or return

    __mkhwt_framework-res-hwext "$THEME_ROOT" "$TEMP_DIR" "$_flag_R"
    or return

    __mkhwt_preview "$THEME_ROOT" "$TEMP_DIR" "$_flag_p"
    or return

    set -ql _flag_T
    or set -l _flag_T $_flag_t
    set -ql _flag_s
    or set -l _flag_s $DEFAULT_SCREEN
    set -ql _flag_v
    or set -l _flag_v $DEFAULT_VERSION
    set -ql _flag_o
    or set -l _flag_o $DEFAULT_FONT
    set -ql _flag_O
    or set -l _flag_O $DEFAULT_FONT_CN
    set -ql _flag_b
    or set -l _flag_b $DEFAULT_BRIEFINFO
    begin
        echo '<?xml version="1.0" encoding="UTF-8" ?>'
        echo '<HwTheme>'
        echo "  <title>$_flag_t</title>"
        echo "  <title-cn>$_flag_T</title-cn>"
        echo "  <author>$_flag_a</author>"
        echo "  <designer>$_flag_d</designer>"
        echo "  <screen>$_flag_s</screen>"
        echo "  <version>$_flag_v</version>"
        echo "  <font>$_flag_o</font>"
        echo "  <font-cn>$_flag_O</font-cn>"
        echo "  <briefinfo>$_flag_b</briefinfo>"
        if set -ql _flag_W
            echo "  <wallpaper-dark>true</wallpaper-dark>"
        end
        if set -ql _flat_m
            and contains "$_flag_m" $FORMAT_TYPES
            echo "  <formatType>$_flag_m</formatType>"
        end
        echo '</HwTheme>'
    end >"$THEME_ROOT/description.xml"
    __mkhwt_log info "Creating theme package..."
    __mkhwt_zip "$THEME_ROOT" "$_flag_t.hwt"
end
